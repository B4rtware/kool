buildscript {
    switch (org.gradle.internal.os.OperatingSystem.current()) {
        case org.gradle.internal.os.OperatingSystem.WINDOWS:
            ext.lwjglNatives = "natives-windows"
            break
        case org.gradle.internal.os.OperatingSystem.LINUX:
            ext.lwjglNatives = "natives-linux"
            break
        case org.gradle.internal.os.OperatingSystem.MAC_OS:
            ext.lwjglNatives = "natives-macos"
            break
    }
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

if (enableAndroid) {
    apply from: 'android.gradle'
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm') {
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }

        fromPreset(presets.js, 'js') {
            compilations.main {
                tasks[compileKotlinTaskName].kotlinOptions {
                    outputFile = "${buildDir}/web/kool.js"
                    moduleKind = "amd"
                    sourceMap = false
                }
            }
        }
    }

    sourceSets {
        // Common
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
            }
        }
        
        // JVM platform
        jvmMain {
            dependencies {
                // dependencies need to be 'api' in order to be found by kool-demo at runtime?!
                api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                api 'org.jetbrains.kotlin:kotlin-reflect'
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

                // only needed for fft audio stuff
                api 'com.github.wendykierp:JTransforms:3.1'

                // lwjgl libraries
                api "org.lwjgl:lwjgl:${lwjglVersion}"
                api "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
                api "org.lwjgl:lwjgl-assimp:${lwjglVersion}"
                api "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
                api "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
                api "org.lwjgl:lwjgl-stb:${lwjglVersion}"
                api "org.lwjgl:lwjgl-vulkan:$lwjglVersion"
                api "org.lwjgl:lwjgl-vma:$lwjglVersion"
                api "org.lwjgl:lwjgl-shaderc:$lwjglVersion"

                // lwjgl native libraries
                api "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
                api "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
                api "org.lwjgl:lwjgl-assimp:${lwjglVersion}:${lwjglNatives}"
                api "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:${lwjglNatives}"
                api "org.lwjgl:lwjgl-opengl:${lwjglVersion}:${lwjglNatives}"
                api "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"
                api "org.lwjgl:lwjgl-vma:${lwjglVersion}:$lwjglNatives"
                api "org.lwjgl:lwjgl-shaderc:${lwjglVersion}:$lwjglNatives"
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        // Javascript platform
        jsMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-js'
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
            }
        }

        sourceSets.all {
            languageSettings {
                progressiveMode = true
                useExperimentalAnnotation('kotlinx.serialization.ImplicitReflectionSerializer')
                useExperimentalAnnotation('kotlinx.coroutines.ExperimentalCoroutinesApi')
                useExperimentalAnnotation('kotlin.contracts.ExperimentalContracts')
                useExperimentalAnnotation('kotlin.ExperimentalUnsignedTypes')
            }
        }
    }
}

/*build.doLast {
    copy {
        includeEmptyDirs = false
        from "${buildDir}/web"
        into "${projectDir}/$webPath"
        include { fileTreeElement ->
            fileTreeElement.path.endsWith(".js")
        }
    }
}*/

//if (publishingAvailable) {
//    apply from: "../publishing.gradle"
//}
