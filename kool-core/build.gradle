apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

if (enableAndroid) {
    apply from: 'android.gradle'
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm') {
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }

        fromPreset(presets.js, 'js') {
            compilations.main {
                tasks[compileKotlinTaskName].kotlinOptions {
                    outputFile = "${buildDir}/web/kool.js"
                    moduleKind = "amd"
                    sourceMap = false
                }
            }
        }
    }

    sourceSets {
        // Common
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
            }
        }
        
        // JVM platform
        jvmMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation 'org.jetbrains.kotlin:kotlin-reflect'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

                // lwjgl libraries
                implementation "org.lwjgl:lwjgl:${lwjglVersion}"
                implementation "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
                implementation "org.lwjgl:lwjgl-assimp:${lwjglVersion}"
                implementation "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
                implementation "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
                
                // only needed for fft audio stuff
                implementation 'com.github.wendykierp:JTransforms:3.1'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'

                implementation "mil.nga:tiff:2.0.0"
                implementation "org.apache.commons:commons-compress:1.17"
            }
        }

        // Javascript platform
        jsMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
            }
        }

        sourceSets.all {
            languageSettings {
                progressiveMode = true
                useExperimentalAnnotation('kotlinx.serialization.ImplicitReflectionSerializer')
                useExperimentalAnnotation('kotlinx.coroutines.ExperimentalCoroutinesApi')
            }
        }
    }
}

build.doLast {
    copy {
        includeEmptyDirs = false
        from "${buildDir}/web"
        into "${projectDir}/$webPath"
        include { fileTreeElement ->
            fileTreeElement.path.endsWith(".js")
        }
    }
}

//if (publishingAvailable) {
//    apply from: "../publishing.gradle"
//}
