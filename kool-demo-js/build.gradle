buildscript {
    ext.webPath = '../docs/kool-js'
}

apply plugin: 'kotlin-platform-js'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile project(":kool-core-js")

    expectedBy project(":kool-demo-common")
}

compileKotlin2Js {
    kotlinOptions.outputFile = "${buildDir}/web/kooldemo.js"
    kotlinOptions.moduleKind = "amd"
    kotlinOptions.sourceMap = false
}

clean {
    delete new File("$projectDir/$webPath")
}

build.doLast {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/$webPath"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }

    copy {
        includeEmptyDirs = false

        from "${buildDir}/web"
        into "${projectDir}/$webPath"
        include { fileTreeElement ->
            fileTreeElement.path.endsWith(".js")
        }
    }

    copy {
        from sourceSets.main.resources.srcDirs
        into "$projectDir/$webPath"
    }
}
