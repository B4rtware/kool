buildscript {
    switch (org.gradle.internal.os.OperatingSystem.current()) {
        case org.gradle.internal.os.OperatingSystem.WINDOWS:
            ext.lwjglNatives = "natives-windows"
            break
        case org.gradle.internal.os.OperatingSystem.LINUX:
            ext.lwjglNatives = "natives-linux"
            break
        case org.gradle.internal.os.OperatingSystem.MAC_OS:
            ext.lwjglNatives = "natives-macos"
            break
    }
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

if (enableAndroid) {
    apply from: 'android.gradle'
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm') {
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }

        fromPreset(presets.js, 'js') {
            compilations.main {
                tasks[compileKotlinTaskName].kotlinOptions {
                    outputFile = "${buildDir}/web/kooldemo.js"
                    moduleKind = "amd"
                    sourceMap = false
                }
            }
        }
    }

    sourceSets {
        // Common
        commonMain {
            dependencies {
                implementation project(":kool-core")
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
            }
        }
        
        // JVM platform
        jvmMain {
            dependencies {
                // fixme: copy dependencies from core-jvm, this should not be needed, but it is (kotlin 1.3.10)
                api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation 'org.jetbrains.kotlin:kotlin-reflect'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

                // only needed for fft audio stuff
                implementation 'com.github.wendykierp:JTransforms:3.1'

                // lwjgl libraries
                implementation "org.lwjgl:lwjgl:${lwjglVersion}"
                implementation "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
                implementation "org.lwjgl:lwjgl-assimp:${lwjglVersion}"
                implementation "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
                implementation "org.lwjgl:lwjgl-opengl:${lwjglVersion}"

                // lwjgl native libraries
                implementation "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
                implementation "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
                implementation "org.lwjgl:lwjgl-assimp:${lwjglVersion}:${lwjglNatives}"
                implementation "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:${lwjglNatives}"
                implementation "org.lwjgl:lwjgl-opengl:${lwjglVersion}:${lwjglNatives}"
            }
        }
        
        // Javascript platform
        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
            }
        }
        
        sourceSets.all {
            languageSettings {
                progressiveMode = true
                useExperimentalAnnotation('kotlinx.serialization.ImplicitReflectionSerializer')
            }
        }
    }
}

clean {
    delete "$projectDir/$webPath"
}

build.doLast {
    // fixme: this is a hack to get the dependencies of core-js as well
    def cfgs = [project(":kool-core").configurations.jsMainImplementation, configurations.jsMainImplementation]
    cfgs.each { cfg ->
        cfg.each { file ->
            copy {
                includeEmptyDirs = false
                from zipTree(file.absolutePath)
                into "${projectDir}/$webPath"
                include { fileTreeElement ->
                    def path = fileTreeElement.path
                    path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
                }
            }
        }
    }
    
    copy {
        includeEmptyDirs = false
        from "${buildDir}/web"
        into "${projectDir}/$webPath"
        include { fileTreeElement ->
            fileTreeElement.path.endsWith(".js")
        }
    }
    
    copy {
        from kotlin.sourceSets.jsMain.resources.srcDirs
        into "$projectDir/$webPath"
    }
}
