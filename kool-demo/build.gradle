buildscript {
    switch (org.gradle.internal.os.OperatingSystem.current()) {
        case org.gradle.internal.os.OperatingSystem.WINDOWS:
            ext.lwjglNatives = "natives-windows"
            break
        case org.gradle.internal.os.OperatingSystem.LINUX:
            ext.lwjglNatives = "natives-linux"
            break
        case org.gradle.internal.os.OperatingSystem.MAC_OS:
            ext.lwjglNatives = "natives-macos"
            break
    }
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm') {
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }

        fromPreset(presets.js, 'js') {
            compilations.main {
                tasks[compileKotlinTaskName].kotlinOptions {
                    outputFile = "${buildDir}/web/kooldemo.js"
                    moduleKind = "amd"
                    sourceMap = false
                }
            }
        }
    }

    sourceSets {
        // Common
        commonMain {
            dependencies {
                implementation project(":kool-core")
            }
        }
        
        // JVM platform
        jvmMain { }
        
        // Javascript platform
        jsMain { }
        
        sourceSets.all {
            languageSettings {
                progressiveMode = true
                useExperimentalAnnotation('kotlinx.serialization.ImplicitReflectionSerializer')
            }
        }
    }
}

clean {
    delete "$projectDir/$webPath"
}

build.doLast {
    // fixme: this is a hack to get the dependencies of core-js as well
    def cfgs = [project(":kool-core").configurations.find { cfg -> cfg.name.endsWith("jsRuntimeClasspath") }, configurations.find { cfg -> cfg.name.endsWith("jsRuntimeClasspath") }]
    cfgs.each { cfg ->
        cfg.each { file ->
            copy {
                includeEmptyDirs = false
                from zipTree(file.absolutePath)
                into "${projectDir}/$webPath"
                include { fileTreeElement ->
                    def path = fileTreeElement.path
                    path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
                }
            }
        }
    }

    copy {
        includeEmptyDirs = false
        from "${buildDir}/web"
        into "${projectDir}/$webPath"
        include { fileTreeElement ->
            fileTreeElement.path.endsWith(".js")
        }
    }

    copy {
        from kotlin.sourceSets.jsMain.resources.srcDirs
        into "$projectDir/$webPath"
    }
}
